type program(info: interval, forms: list<form>)

type pattern =
  | word(info: interval, word: text)

type form =
  | rule(info: interval, arity: text, pattern: pattern, replacement: list<expression>)
  | variable(info: interval, name: text)
  | number(info: interval, digits: text)
  | atom(info: interval, contents: text)
  | string(info: interval, contents: text)
  | block(info: interval, expressions: list<expression>)
  | text(info: interval, contents: text)

grammar Belle : program {
  program =
    | xs:form* end -> program(meta, xs)

  form =
    | ":" s x:special_form                                              -> x
    | e:expr                                                            -> e
    | t:text_piece                                                      -> t

  text_piece =
    | w:word                                                            -> form.text(meta, w)
    | p:punctuation                                                     -> form.text(meta, p)
    | s:horizontal_spaces                                               -> form.text(meta, s)
    | n:newline                                                         -> form.text(meta, n)

  special_form =
    | id<"rule"> p:ws<pattern> ws<"/"> a:ws<number> ws<"="> e:ws<expr_block> ws<"."> ws<newline>  -> form.rule(meta, a, p, e)

  pattern =
    | w:word -> pattern.word(meta, w)

  expr =
    | "%" w:word                -> form.variable(meta, w)
    | "#" n:number              -> form.number(meta, n)
    | "#" w:word                -> form.atom(meta, w)
    | s:string                  -> form.string(meta, s)
    | ~"." t:text_piece         -> t
    | "[" s e:expr_block s "]"  -> form.block(meta, e)
  
  expr_block =
    | listOf<expr, space+>

  // -- Lexical stuff  
  token word =
    | "@"? (letter | digit | "-" | "_")+
  
  token number =
    | digit+

  string =
    | "\"" x:string_chars "\"" -> x
  
  token string_chars =
    | (~"\"" any)*

  token newline =
    | "\r\n"
    | "\r"
    | "\n"

  token horizontal_space =
    | " "
    | "\t"

  token s = horizontal_space*

  token horizontal_spaces = horizontal_space+

  token punctuation =
    | "." | "," | "!" | "?" | "," | ";" | "(" | ")" | "-"

  id<word> = x:word ~(letter | digit | "-" | "_") -> x
  ws<pattern> = s x:pattern -> x
}